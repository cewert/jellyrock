import "pkg:/source/utils/config.bs"

sub init()
  m.top.itemComponentName = "ConfigItem"

  m.top.drawFocusFeedback = True
  m.top.vertFocusAnimationStyle = "floatingFocus"

  m.top.observeField("itemSelected", "onItemSelected")

  m.top.itemSize = [750, 75]
  m.top.itemSpacing = [0, 25]

  m.top.setfocus(true)

end sub

sub setData()
  items = m.top.configItems
  data = CreateObject("roSGNode", "ContentNode")
  data.appendChildren(items)

  m.top.content = data
end sub

sub onItemSelected()
  i = m.top.itemSelected
  itemField = m.top.content.getchild(i)

  configListShowDialog(itemField)
end sub

function onDialogButton()
  d = m.dialog
  buttonText = d.buttons[d.buttonSelected]

  if buttonText = tr("OK")
    m.configField.value = d.text
    dismissDialog()
    return true
  else if buttonText = tr("Cancel")
    dismissDialog()
    return true
  end if
  return false
end function


sub configListShowDialog(configField)
  dialog = createObject("roSGNode", "StandardKeyboardDialog")
  m.configField = configField
  dialog.title = configField.label
  dialog.buttons = [tr("OK"), tr("Cancel")]
  m.greenPalette = createObject("roSGNode", "RSGPalette")
  m.greenPalette.colors = {
    DialogBackgroundColor: "#2A2B2A"
  }
  dialog.palette = m.greenPalette

  if configField.type = "password"
    dialog.textEditBox.secureMode = true
  end if

  if configField.value <> ""
    dialog.text = configField.value
  end if

  m.top.getscene().dialog = dialog
  m.dialog = dialog

  dialog.observeField("buttonSelected", "onDialogButton")
end sub

sub dismissDialog()
  m.dialog.close = true
end sub

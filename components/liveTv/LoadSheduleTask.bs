import "pkg:/source/api/baserequest.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/misc.bs"

sub init()
  m.top.functionName = "loadSchedule"
end sub

sub loadSchedule()

  results = []

  params = {
    UserId: m.global.user.id,
    SortBy: "startDate",
    EnableImages: false,
    EnableTotalRecordCount: false,
    EnableUserData: false,
    channelIds: m.top.channelIds,
    MaxStartDate: m.top.endTime,
    MinEndDate: m.top.startTime
  }

  url = "LiveTv/Programs"

  resp = APIRequest(url)
  data = postJson(resp, FormatJson(params))

  if not isValid(data)
    m.top.schedule = results
    return
  end if

  results = []

  for each item in data.Items
    program = createObject("roSGNode", "ScheduleProgramData")
    program.json = item
    ' Are we currently recording this program?
    if isValid(program.json.TimerId) and program.json.TimerId <> ""
      program.hdSmallIconUrl = "pkg:/images/red.png"
    else
      program.hdSmallIconUrl = invalid
    end if
    results.push(program)
  end for


  m.top.schedule = results

end sub

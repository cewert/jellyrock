sub init()
  m.content = m.top.findNode("content")
  m.top.observeField("contentData", "onContentDataChanged")

  m.top.id = "OKDialog"
  m.top.height = 900
  m.top.title = "What's New?"
  m.top.buttons = [tr("OK")]

  colorConstants = m.global.constants.colors

  m.dialogStyles = {
    "default": {
      "fontSize": 27,
      "fontUri": "font:BoldSystemFontFile",
      "color": colorConstants.text_primary
    },
    "b": {
      "fontSize": 27,
      "fontUri": "font:SystemFontFile",
      "color": colorConstants.text_secondary
    },
    "header": {
      "fontSize": 35,
      "fontUri": "font:SystemFontFile",
      "color": colorConstants.secondary
    },
    "p": {
      "fontSize": 27,
      "fontUri": "font:SystemFontFile",
      "color": colorConstants.text_primary
    }
  }

end sub

sub onContentDataChanged()
  for each item in m.top.contentData.data
    textLine = m.content.CreateChild("StdDlgMultiStyleTextItem")
    textLine.drawingStyles = m.dialogStyles
    textLine.text = item
  end for
end sub

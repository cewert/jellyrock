import "pkg:/source/api/baserequest.bs"
import "pkg:/source/api/Image.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/misc.bs"

sub setData()
  ' We keep json around just as a reference,
  ' but ideally everything should be going through one of the interfaces
  datum = m.top.json

  m.top.id = datum.Id
  m.top.name = datum.name
  m.top.Type = datum.Type

  if not isValid(datum.collectionType)
    m.top.collectionType = datum.Type
  else
    m.top.collectionType = datum.collectionType
  end if

  ' Set appropriate Images for Wide and Tall based on type

  if LCase(datum.Type) = "collectionfolder" or LCase(datum.Type) = "userview"
    if IsValid(datum.ImageTags)
      params = { "Tag": datum.ImageTags.Primary, "maxHeight": 261, "maxWidth": 464 }
      m.top.thumbnailURL = ImageURL(datum.Id, "Primary", params)
      m.top.widePosterUrl = m.top.thumbnailURL
    end if

    ' Add Icon URLs for display if there is no Poster
    if LCase(m.top.collectionType) = "livetv"
      m.top.iconUrl = "pkg:/images/media_type_icons/live_tv_white.png"
    else if LCase(m.top.collectionType) = "folders"
      m.top.iconUrl = "pkg:/images/media_type_icons/folder_white.png"
    end if

  else if datum.Type = "Episode" or LCase(datum.Type) = "recording" or datum.Type = "MusicVideo"
    m.top.isWatched = datum.UserData.Played

    imgParams = {}
    imgParams.Append({ "maxHeight": 261 })
    imgParams.Append({ "maxWidth": 464 })

    if isValid(datum.ImageTags.Primary)
      param = { "Tag": datum.ImageTags.Primary }
      imgParams.Append(param)
    end if

    m.top.thumbnailURL = ImageURL(datum.Id, "Primary", imgParams)

    ' Add Wide Poster  (Series Backdrop)
    if isValid(datum.ParentThumbImageTag)
      imgParams["Tag"] = datum.ParentThumbImageTag
      m.top.widePosterUrl = ImageURL(datum.ParentThumbItemId, "Thumb", imgParams)
    else if isValid(datum.ParentBackdropImageTags)
      imgParams["Tag"] = datum.ParentBackdropImageTags[0]
      m.top.widePosterUrl = ImageURL(datum.ParentBackdropItemId, "Backdrop", imgParams)
    else if isValid(datum.ImageTags.Primary)
      imgParams["Tag"] = datum.SeriesPrimaryImageTag
      m.top.widePosterUrl = ImageURL(datum.Id, "Primary", imgParams)
    end if

  else if datum.Type = "Series"
    m.top.isWatched = datum.UserData.Played
    imgParams = { "maxHeight": 261 }
    imgParams.Append({ "maxWidth": 464 })

    m.top.posterURL = ImageURL(datum.Id, "Primary", imgParams)

    ' Add Wide Poster  (Series Backdrop)
    if isValid(datum.ImageTags) and isValid(datum.imageTags.Thumb)
      imgParams["Tag"] = datum.imageTags.Thumb
      m.top.widePosterUrl = ImageURL(datum.Id, "Thumb", imgParams)
    else if isValid(datum.BackdropImageTags)
      imgParams["Tag"] = datum.BackdropImageTags[0]
      m.top.widePosterUrl = ImageURL(datum.Id, "Backdrop", imgParams)
    end if

  else if datum.Type = "Movie"
    m.top.isWatched = datum.UserData.Played

    imgParams = {}
    imgParams.Append({ "maxHeight": 261 })
    imgParams.Append({ "maxWidth": 175 })

    if isValid(datum.ImageTags.Primary)
      param = { "Tag": datum.ImageTags.Primary }
      imgParams.Append(param)
    end if

    m.top.posterURL = ImageURL(datum.Id, "Primary", imgParams)

    ' For wide image, use backdrop
    imgParams["maxWidth"] = 464

    if isValid(datum.ImageTags) and isValid(datum.imageTags.Thumb)
      imgParams["Tag"] = datum.imageTags.Thumb
      m.top.thumbnailUrl = ImageURL(datum.Id, "Thumb", imgParams)
    else if isValid(datum.BackdropImageTags[0])
      imgParams["Tag"] = datum.BackdropImageTags[0]
      m.top.thumbnailUrl = ImageURL(datum.Id, "Backdrop", imgParams)
    end if
  else if datum.Type = "Video"
    m.top.isWatched = datum.UserData.Played

    imgParams = {
      "maxHeight": 261,
      "maxWidth": 464
    }

    if isValid(datum.ImageTags) and isValid(datum.ImageTags.Primary)
      imgParams.Append({ "Tag": datum.ImageTags.Primary })
    end if

    m.top.posterURL = ImageURL(datum.Id, "Primary", imgParams)
    m.top.thumbnailUrl = m.top.posterURL
  else if datum.Type = "MusicAlbum"
    params = { "Tag": datum.ImageTags.Primary, "maxHeight": 261, "maxWidth": 261 }
    m.top.thumbnailURL = ImageURL(datum.Id, "Primary", params)
    m.top.widePosterUrl = m.top.thumbnailURL
    m.top.posterUrl = m.top.thumbnailURL
  else if datum.Type = "TvChannel" or datum.Type = "Channel"
    params = { "Tag": datum.ImageTags.Primary, "maxHeight": 261, "maxWidth": 464 }
    m.top.thumbnailURL = ImageURL(datum.Id, "Primary", params)
    m.top.widePosterUrl = m.top.thumbnailURL
    m.top.iconUrl = "pkg:/images/media_type_icons/live_tv_white.png"
  else if datum.Type = "Photo"
    params = { "Tag": datum.ImageTags.Primary, "maxHeight": 261, "maxWidth": 464 }

    m.top.thumbnailURL = ImageURL(datum.Id, "Primary", params)
    m.top.widePosterUrl = m.top.thumbnailURL
    m.top.posterUrl = m.top.thumbnailURL
  else if datum.Type = "PhotoAlbum"
    params = { "Tag": datum.ImageTags.Primary, "maxHeight": 261, "maxWidth": 464 }

    m.top.thumbnailURL = ImageURL(datum.Id, "Primary", params)
    m.top.widePosterUrl = m.top.thumbnailURL
    m.top.posterUrl = m.top.thumbnailURL
  end if
end sub

import "pkg:/source/api/baserequest.bs"
import "pkg:/source/api/Image.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/misc.bs"

sub setFields()
  json = m.top.json

  m.top.id = json.id
  m.top.Title = json.name
  m.top.overview = json.overview
  m.top.Description = json.overview
  m.top.favorite = json.UserData.isFavorite
  m.top.watched = json.UserData.played
  m.top.Type = "Boxset"

  setPoster()
end sub

sub setPoster()
  if isValid(m.top.image)
    m.top.posterURL = m.top.image.url
  else
    if isValid(m.top.json.ImageTags) and isValidAndNotEmpty(m.top.json.ImageTags.Primary)
      imgParams = { "maxHeight": 440, "maxWidth": 295, "Tag": m.top.json.ImageTags.Primary }
      m.top.posterURL = ImageURL(m.top.json.id, "Primary", imgParams)
    else if isValid(m.top.json.BackdropImageTags) and isValidAndNotEmpty(m.top.json.BackdropImageTags[0])
      imgParams = { "maxHeight": 440, "Tag": m.top.json.BackdropImageTags[0] }
      m.top.posterURL = ImageURL(m.top.json.id, "Backdrop", imgParams)
    else
      ' Set posterURL to empty string when no valid tags exist to prevent 404 requests
      m.top.posterURL = ""
    end if

    ' Add Backdrop Image
    if isValid(m.top.json.BackdropImageTags) and isValidAndNotEmpty(m.top.json.BackdropImageTags[0])
      imgParams = { "maxHeight": 720, "maxWidth": 1280, "Tag": m.top.json.BackdropImageTags[0] }
      m.top.backdropURL = ImageURL(m.top.json.id, "Backdrop", imgParams)
    else
      ' Set backdropURL to empty string when no valid backdrop tags exist
      m.top.backdropURL = ""
    end if
  end if
end sub

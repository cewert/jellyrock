import "pkg:/source/api/baserequest.bs"
import "pkg:/source/api/Image.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/misc.bs"

sub setFields()
  json = m.top.json
  m.top.id = json.id
  if isValid(json.number)
    m.top.title = `${tr("CH")} ${json.number} ${json.name}`
  else
    m.top.title = json.name
  end if
  m.top.live = true
  m.top.Type = "TvChannel"
  setPoster()
end sub

sub setPoster()
  if isValid(m.top.image)
    m.top.posterURL = m.top.image.url
  else if isValid(m.top.json.ImageTags) and isValidAndNotEmpty(m.top.json.ImageTags.Primary)
    imgParams = { "maxHeight": 60, "tag": m.top.json.ImageTags.Primary }
    m.top.hdsmalliconurl = ImageURL(m.top.json.id, "Primary", imgParams)

    imgParams = { "maxHeight": 440, "maxWidth": 295, "tag": m.top.json.ImageTags.Primary }
    m.top.posterURL = ImageURL(m.top.json.id, "Primary", imgParams)
  else
    ' Set URLs to empty strings when no valid tags exist to prevent 404 requests
    ' Note: Live TV channels may have images without ImageTags - handled by ImageURL function
    m.top.posterURL = ""
    m.top.hdsmalliconurl = ""
  end if
end sub

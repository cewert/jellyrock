import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/misc.bs"

sub setDataFromJSON()
  json = m.top.json
  loadFromJSON(json)
end sub

sub loadFromJSON(json)
  m.top.id = json.User.id

  m.top.username = json.User.name
  m.top.token = json.AccessToken
end sub

sub loadFromRegistry(id as string)
  m.top.id = id

  m.top.username = getUserSetting("username")
  m.top.token = getUserSetting("token")
end sub

sub saveToRegistry()
  users = parseJson(getSetting("available_users", "[]"))
  thisUser = invalid
  for each user in users
    if user.id = m.top.id then thisUser = user
  end for
  if not isValid(thisUser)
    users.push({
      id: m.top.id,
      username: m.top.username,
      server: m.global.session.server.url
    })
    setSetting("available_users", formatJson(users))
  end if
end sub

sub removeFromRegistry()
  newUsers = []
  users = parseJson(getSetting("available_users", "[]"))
  for each user in users
    if m.top.id <> user.id then newUsers.push(user)
  end for

  setSetting("available_users", formatJson(newUsers))
end sub

function getPreference(key as string)
  return getUserSetting("pref-" + key)
end function

function setPreference(key as string, value as string)
  return setUserSetting("pref-" + key, value)
end function

sub setServer(hostname as string)
  m.top.server = hostname
end sub

import "pkg:/source/api/baserequest.bs"
import "pkg:/source/api/Image.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/misc.bs"

sub setFields()
  json = m.top.json

  startDate = createObject("roDateTime")
  endDate = createObject("roDateTime")
  startDate.FromISO8601String(json.StartDate)
  endDate.FromISO8601String(json.EndDate)

  m.top.Title = json.Name
  m.top.PlayStart = startDate.AsSeconds()
  m.top.PlayDuration = endDate.AsSeconds() - m.top.PlayStart
  m.top.Id = json.Id
  m.top.Description = json.overview
  m.top.EpisodeTitle = json.EpisodeTitle
  m.top.isLive = json.isLive
  m.top.isRepeat = json.isRepeat
  m.top.startDate = json.startDate
  m.top.endDate = json.endDate
  m.top.channelId = json.channelId

  if isValid(json.IsSeries) and json.IsSeries = true
    if isValid(json.IndexNumber)
      m.top.episodeNumber = json.IndexNumber
    end if

    if isValid(json.ParentIndexNumber)
      m.top.seasonNumber = json.ParentIndexNumber
    end if
  end if

  setPoster()
end sub

sub setPoster()
  if isValid(m.top.image)
    m.top.posterURL = m.top.image.url
  else
    if isValid(m.top.json.ImageTags) and isValidAndNotEmpty(m.top.json.ImageTags.Thumb)
      imgParams = { "maxHeight": 500, "maxWidth": 500, "tag": m.top.json.ImageTags.Thumb }
      m.top.posterURL = ImageURL(m.top.json.id, "Thumb", imgParams)
    else
      ' Set posterURL to empty string when no valid thumb tags exist to prevent 404 requests
      m.top.posterURL = ""
    end if
  end if
end sub

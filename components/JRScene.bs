import "pkg:/source/roku_modules/log/LogMixin.brs"
import "pkg:/source/utils/misc.bs"
import "pkg:/source/utils/backdrop.bs"

sub init()
  m.log = log.Logger("JRScene")
  m.top.backgroundColor = m.global.constants.colorBackgroundPrimary
  m.top.backgroundURI = ""
  m.loadingText = m.top.findNode("loadingText")
  m.spinner = m.top.findNode("spinner")
  m.imageFader = m.top.findNode("imageFader")
  localDevice = m.global.device
  m.imageFader.width = localDevice.uiResolution[0]
  m.imageFader.height = localDevice.uiResolution[1]

  ' Set initial visibility based on user setting
  localUser = m.global.user
  if isValid(localUser)
    m.showBackdrop = resolveShowBackdrop(localUser.settings, localUser.config)
  else
    m.showBackdrop = true ' Default to showing backdrops if user not loaded
  end if
  m.imageFader.visible = m.showBackdrop

  ' set text manually to AVOID translation
  defaultFont = m.top.findNode("defaultFont")
  fallbackFont = m.top.findNode("fallbackFont")
  defaultFont.text = "Ag"
  fallbackFont.text = "Ag"
end sub

sub loadingTextChanged()
  m.loadingText.text = m.top.loadingText
end sub

sub backgroundImageUriChanged()
  if not isValid(m.imageFader) then return

  ' Only update URI if backdrops are enabled (performance optimization)
  if m.showBackdrop
    m.imageFader.isAnimated = true
    m.imageFader.uri = m.top.backgroundImageUri
  end if
end sub

' Set the background image with animation control
' @param {string} uri - The image URI to display
' @param {boolean} isAnimated - Whether to animate the transition
sub setBackgroundImage(uri as string, isAnimated = true as boolean)
  if not isValid(m.imageFader) then return

  ' Only update URI if backdrops are enabled (performance optimization)
  if m.showBackdrop
    m.imageFader.isAnimated = isAnimated
    m.imageFader.uri = uri
  end if
end sub

' Triggered when the isLoading boolean component field is changed
sub isLoadingChanged()
  ' toggle visibility of active view/group
  group = m.global.sceneManager.callFunc("getActiveScene")
  if isValid(group)
    group.visible = not m.top.disableRemote
  end if

  ' toggle visibility of loading spinner
  m.spinner.visible = m.top.isLoading
  ' toggle visibility of loading text
  m.loadingText.visible = m.top.isLoading
end sub

function onKeyEvent(key as string, press as boolean) as boolean
  if not press then return false
  if m.top.disableRemote then return true

  if key = "back"
    m.global.sceneManager.callFunc("popScene")
    return true
  else if key = "options"
    group = m.global.sceneManager.callFunc("getActiveScene")
    if isValid(group) and isValid(group.optionsAvailable) and group.optionsAvailable
      group.lastFocus = group.focusedChild
      panel = group.findNode("options")
      panel.visible = true
      panel.findNode("panelList").setFocus(true)
    end if
    return true
  end if

  return false
end function

import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/misc.bs"

sub init()
  m.itemPoster = m.top.findNode("itemPoster")
  m.posterText = m.top.findNode("posterText")
  initTitle()
  m.backdrop = m.top.findNode("backdrop")

  m.itemPoster.observeField("loadStatus", "onPosterLoadStatusChanged")

  'Parent is MarkupGrid and it's parent is the ItemGrid
  m.topParent = m.top.GetParent().GetParent()

  m.title.visible = false

  'Get the imageDisplayMode for these grid items
  if isValid(m.topParent.imageDisplayMode)
    m.itemPoster.loadDisplayMode = m.topParent.imageDisplayMode
  end if
end sub

sub initTitle()
  m.title = m.top.findNode("title")
end sub

sub itemContentChanged()
  m.title.visible = false

  if isValid(m.topParent.showItemTitles)
    if LCase(m.topParent.showItemTitles) = "showalways"
      m.title.visible = true
    end if
  end if

  itemData = m.top.itemContent

  if not isValid(itemData) then return
  userSettings = m.global.user.settings

  if isValid(itemData.json)
    if not userSettings.uiTvShowsDisableUnwatchedCount
      if isValid(itemData.json.UserData)
        if isValid(itemData.json.UserData.Played) and itemData.json.UserData.Played = true
          m.itemPoster.isWatched = true
        else if isValid(itemData.json.UserData.UnplayedItemCount) and itemData.json.UserData.UnplayedItemCount > 0
          m.itemPoster.unplayedCount = itemData.json.UserData.UnplayedItemCount
        end if
      end if
    end if
  end if

  m.itemPoster.uri = itemData.PosterUrl
  m.posterText.text = itemData.title
  m.title.text = itemData.title

  'If Poster not loaded, ensure "blue box" is shown until loaded
  if m.itemPoster.loadStatus <> "ready"
    m.backdrop.visible = true
    m.posterText.visible = true
  end if
end sub

sub focusChanged()
  if not isValid(m.title) then initTitle()

  if m.top.itemHasFocus = true
    m.title.repeatCount = -1
  else
    m.title.repeatCount = 0
  end if

  if isValid(m.topParent.showItemTitles)
    if LCase(m.topParent.showItemTitles) = "showonhover"
      m.title.visible = m.top.itemHasFocus
    end if
  end if
end sub

'Hide backdrop and text when poster loaded
sub onPosterLoadStatusChanged()
  if m.itemPoster.loadStatus = "ready"
    m.backdrop.visible = false
    m.posterText.visible = false
  end if
end sub

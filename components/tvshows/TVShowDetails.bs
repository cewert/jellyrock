import "pkg:/source/roku_modules/log/LogMixin.brs"
import "pkg:/source/utils/backdropUtils.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/misc.bs"

sub init()
  m.log = log.Logger("TVShowDetails")
  m.top.optionsAvailable = false
  main = m.top.findNode("toplevel")
  main.translation = [96, 175]
  m.tvshowPoster = m.top.findNode("tvshowPoster")
  m.extrasSlider = m.top.findNode("tvSeasonExtras")
  m.getShuffleEpisodesTask = createObject("roSGNode", "getShuffleEpisodesTask")
  m.shuffle = m.top.findNode("shuffle")
  m.extrasSlider.visible = true
  m.seasons = m.top.findNode("seasons")
  m.overview = m.top.findNode("overview")

  m.overview.ellipsisText = tr("... (Press * to read more)")

  ' Set initial focus for scene navigation
  m.top.lastFocus = m.shuffle
end sub

sub OnScreenShown()
  ' Set backdrop from series data
  ' This handles the case when returning to an already-loaded screen
  if isValid(m.top.itemContent)
    if isValid(m.top.itemContent.backdropUrl)
      m.global.sceneManager.callFunc("setBackgroundImage", m.top.itemContent.backdropUrl)
      m.log.info("backdropUrl set to", m.top.itemContent.backdropUrl)
    end if
  else
    m.log.info("cannot set backdropUrl, m.top.itemContent is invalid")
  end if

  ' Restore focus for scene navigation
  if isValid(m.top.lastFocus)
    m.top.lastFocus.setFocus(true)
  else
    m.top.setFocus(true)
  end if
end sub

sub itemContentChanged()
  ' Updates video metadata
  ' TODO - make things use item rather than itemData
  item = m.top.itemContent
  print "[TVShowDetails] itemContentChanged called"
  if not isValid(item) then return

  itemData = item.json

  ' Set backdrop from series data
  print "[TVShowDetails] backdropUrl in itemContentChanged: "; item.backdropUrl
  if isValid(item.backdropUrl)
    print "[TVShowDetails] Setting backdrop: "; item.backdropUrl
    m.global.sceneManager.callFunc("setBackgroundImage", item.backdropUrl)
  else
    print "[TVShowDetails] backdropUrl not valid, setting empty"
    m.global.sceneManager.callFunc("setBackgroundImage", "")
  end if

  m.tvshowPoster.uri = m.top.itemContent.posterURL

  ' Handle all "As Is" fields
  m.top.overhangTitle = itemData.name

  'Check production year, if invalid remove label
  if isValid(itemData.productionYear)
    setFieldText("releaseYear", itemData.productionYear)
  else
    m.top.findNode("main_group").removeChild(m.top.findNode("releaseYear"))
  end if

  'Check officialRating, if invalid remove label
  if isValid(itemData.officialRating)
    setFieldText("officialRating", itemData.officialRating)
  else
    m.top.findNode("main_group").removeChild(m.top.findNode("officialRating"))
  end if

  'Check communityRating, if invalid remove label
  if isValid(itemData.communityRating)
    m.top.findNode("star").visible = true
    setFieldText("communityRating", int(itemData.communityRating * 10) / 10)
  else
    m.top.findNode("main_group").removeChild(m.top.findNode("communityRating"))
    m.top.findNode("main_group").removeChild(m.top.findNode("star"))
    m.top.findNode("star").visible = false
  end if

  setFieldText("overview", itemData.overview)

  if type(itemData.RunTimeTicks) = "LongInteger"
    setFieldText("runtime", stri(getRuntime()) + " mins")
  end if

  'History feild is set via the function getHistory()
  setFieldText("history", getHistory())

  'Check genres, if invalid remove label
  if itemData.genres.count() > 0
    setFieldText("genres", itemData.genres.join(", "))
  else
    m.top.findNode("main_group").removeChild(m.top.findNode("genres"))
  end if

  'We don't display Directors in the show page. Might want to remove this.
  for each person in itemData.people
    if person.type = "Director"
      exit for
    end if
  end for
  if itemData.taglines.count() > 0
    setFieldText("tagline", itemData.taglines[0])
  else
    m.top.findNode("main_group").removeChild(m.top.findNode("tagline"))
  end if
end sub

sub setFieldText(field, value)
  node = m.top.findNode(field)
  if not isValid(node) or not isValid(value) then return

  ' Handle non strings... Which _shouldn't_ happen, but hey
  if type(value) = "roInt" or type(value) = "Integer"
    value = str(value).trim()
  else if type(value) = "roFloat" or type(value) = "Float"
    value = str(value).trim()
  else if type(value) <> "roString" and type(value) <> "String"
    value = ""
  end if

  node.text = value
end sub

function getRuntime() as integer
  itemData = m.top.itemContent.json

  ' A tick is .1ms, so 1/10,000,000 for ticks to seconds,
  ' then 1/60 for seconds to minutess... 1/600,000,000
  return round(itemData.RunTimeTicks / 600000000.0)
end function

function getEndTime() as string
  itemData = m.top.itemContent.json

  date = CreateObject("roDateTime")
  duration_s = int(itemData.RunTimeTicks / 10000000.0)
  date.fromSeconds(date.asSeconds() + duration_s)
  date.toLocalTime()

  return formatTime(date)
end function

function getHistory() as string
  itemData = m.top.itemContent.json
  ' Aired Fridays at 9:30 PM on ABC (US)

  airwords = invalid
  studio = invalid
  if itemData.status = "Ended"
    verb = "Aired"
  else
    verb = "Airs"
  end if

  airdays = itemData.airdays
  airtime = itemData.airtime
  if isValid(airtime) and airdays.count() = 1
    airwords = airdays[0] + " at " + airtime
  end if

  if itemData.studios.count() > 0
    studio = itemData.studios[0].name
  end if

  if not isValid(studio) and not isValid(airwords)
    m.top.findNode("main_group").removeChild(m.top.findNode("history"))
    return ""
  end if

  words = verb
  if isValid(airwords)
    words = words + " " + airwords
  end if
  if isValid(studio)
    words = words + " on " + studio
  end if

  return words
end function

function round(f as float) as integer
  ' BrightScript only has a "floor" round
  ' This compares floor to floor + 1 to find which is closer
  m = int(f)
  n = m + 1
  x = abs(f - m)
  y = abs(f - n)
  if y > x
    return m
  else
    return n
  end if
end function

sub onShuffleEpisodeDataLoaded()
  m.getShuffleEpisodesTask.unobserveField("data")

  if isValid(m.getShuffleEpisodesTask.data)
    queueManager = m.global.queueManager
    queueManager.callFunc("set", m.getShuffleEpisodesTask.data.items)
    queueManager.callFunc("playQueue")
  end if
end sub

function onKeyEvent(key as string, press as boolean) as boolean
  if key = "OK" or key = "play"
    if m.shuffle.hasFocus()
      m.getShuffleEpisodesTask.showID = m.top.itemContent.id
      m.getShuffleEpisodesTask.observeField("data", "onShuffleEpisodeDataLoaded")
      m.getShuffleEpisodesTask.control = "RUN"
      return true
    end if
  end if

  if not press then return false

  if key = "options"
    if m.overview.isTextEllipsized
      if isAllValid([m.top.itemContent.json.name, m.top.itemContent.json.overview])
        m.global.sceneManager.callFunc("standardDialog", m.top.itemContent.json.name, { data: ["<p>" + m.top.itemContent.json.overview + "</p>"] })
        return true
      end if
    end if
    return false
  end if

  topGrp = m.top.findNode("seasons")
  bottomGrp = m.top.findNode("extrasGrid")

  if key = "down" and m.overview.hasFocus()
    m.shuffle.setFocus(true)
    return true
  else if key = "down" and m.shuffle.hasFocus()
    topGrp.setFocus(true)
    return true
  else if key = "down" and topGrp.hasFocus()
    bottomGrp.setFocus(true)
    m.top.findNode("VertSlider").reverse = false
    m.top.findNode("pplAnime").control = "start"
    return true
  else if key = "up" and bottomGrp.hasFocus()
    if bottomGrp.itemFocused = 0
      m.top.findNode("VertSlider").reverse = true
      m.top.findNode("pplAnime").control = "start"
      topGrp.setFocus(true)
      return true
    end if
  else if key = "up" and topGrp.hasFocus()
    m.shuffle.setFocus(true)
    return true
  else if key = "up" and m.shuffle.hasFocus()
    m.overview.setFocus(true)
    return true
  else if key = "play" and m.seasons.hasFocus()
    print "play was pressed from the seasons row"
    if isValid(m.seasons.TVSeasonData) and isValid(m.seasons.TVSeasonData.Items)
      itemFocused = m.seasons.rowItemFocused
      m.top.quickPlayNode = m.seasons.TVSeasonData.Items[itemFocused[1]]
      return true
    end if
  else if key = "play" and m.extrasSlider.isInFocusChain()
    print "play was pressed from the extras grid"
    extrasGrid = m.top.findNode("extrasGrid")
    if isValid(extrasGrid.focusedItem)
      m.top.quickPlayNode = extrasGrid.focusedItem
      return true
    end if
  end if

  return false
end function
